* init-capture-hugo.el
:PROPERTIES:
:HEADER-ARGS: :tangle (concat temporary-file-directory "init-capture-hugo.el") :lexical t
:END:

** Headers

#+BEGIN_SRC emacs-lisp
  ;;; init-capture-hugo.el -- Org Capture and Hugo Integration -*- lexical-binding: t; -*-

  ;; Filename: init-capture-hugo.el
  ;; Description: Org Capture Templates and Hugo Blog Integration
  ;; Author: mcge <mcgeq@outlook.com>
  ;; Copyright (C) 2024-2025, mcge, all rights reserved.
  ;; Create   Date: 2025-01-04 15:00:00
  ;; Version: 2.0
  ;; Modified   By: mcgeq <mcgeq@outlook.com>
  ;; Last Modified: <2025-02-16 Sun 12:00>
  ;; Keywords: org, capture, hugo, blog
  ;; Compatibility: GNU Emacs 31.0.50+

  ;;; Commentary:
  ;;
  ;; Org Capture 模板和 Hugo 博客集成配置。
  ;;
  ;; 本文件包含:
  ;; 1. Org Capture 模板定义
  ;; 2. Hugo 博客集成
  ;; 3. 密码管理辅助函数
  ;; 4. Easy Hugo 配置
  ;;
  ;; 使用方法:
  ;; (require 'init-org-vars)        ; 先加载变量定义
  ;; (require 'init-capture-hugo)    ; 再加载 capture 配置
  ;;

  ;;; Code:
#+END_SRC


** Require
#+BEGIN_SRC emacs-lisp
  ;;; Dependencies:
  (require 'init-org-vars)
  (require 'ox)
  (require 'org-capture)
  
  ;; 按需加载 Hugo 集成
  (when mcg-org-enable-hugo-integration
    (require 'ox-hugo nil t)
    (require 'easy-hugo nil t))

#+END_SRC

** Code

*** 1. Basic Capture Configuration
#+BEGIN_SRC emacs-lisp
  ;;; ====================================================================
  ;;; 1. 基础 Capture 配置
  ;;; ====================================================================

  ;; Hugo 基础目录配置
  (when mcg-org-enable-hugo-integration
    (setq org-hugo-base-dir (concat mcg-blog-base-directory "/")))
  ;; Capture mode hooks
  (add-hook 'org-capture-mode-hook
            (lambda ()
              (setq-local org-complete-tags-always-offer-all-agenda-tags t)))
  (add-hook 'org-capture-mode-hook 'delete-other-windows)

  ;; 不使用 agenda 日期
  (setq org-capture-use-agenda-date nil)

#+END_SRC

*** 2. Capture Templates
#+BEGIN_SRC emacs-lisp
  ;;; ====================================================================
  ;;; 2. Capture 模板定义
  ;;; ====================================================================

  ;; 初始化模板列表
  (setq org-capture-templates nil)

  ;; 任务相关模板
  (add-to-list 'org-capture-templates '("t" "Tasks"))
  (add-to-list 'org-capture-templates
               '("tr" "Book Reading Task"
                 entry (file+olp mcg-org-task-file "Reading Book")
                 "* TODO %^{书名}\n%u\n%a\n"
                 :clock-in t))
  (add-to-list 'org-capture-templates
               '("tw" "Work Task"
                 entry (file+headline mcg-org-task-file "Work")
                 "* TODO %^{任务名}\n%u\n%a\n"
                 :clock-in t :clock-resume t))

  ;; 日记相关模板
  (add-to-list 'org-capture-templates '("d" "Diary"))
  (add-to-list 'org-capture-templates
               '("dt" "Today's TODO List"
                 entry (file+olp+datetree mcg-org-diary-file)
                 "* Today's TODO List [/]\n%T\n\n** TODO %?"
                 :empty-lines 1
                 :jump-to-captured t))
  (add-to-list 'org-capture-templates
               '("do" "Other stuff"
                 entry (file+olp+datetree mcg-org-diary-file)
                 "* %?\n%T\n\n%i"
                 :empty-lines 1
                 :jump-to-captured t))

  ;; 笔记和日志模板
  (add-to-list 'org-capture-templates
               '("n" "Notes"
                 entry (file mcg-org-default-notes-file)
                 "* %^{heading} %t %^g\n %?\n"
                 :empty-lines 1))

  (add-to-list 'org-capture-templates
               '("j" "Journal"
                 entry (file+datetree mcg-org-journal-file)
                 "* %U - %^{heading} %^g\n %?\n"
                 :empty-lines 1))

  ;; 账单模板
  (add-to-list 'org-capture-templates
               '("b" "Billing"
                 plain (file+function mcg-org-billing-file find-month-tree)
                 " | %U | %^{类别} | %^{描述} | %^{金额} |"
                 :kill-buffer t))

  ;; 密码管理模板
  (add-to-list 'org-capture-templates
               '("p" "Passwords"
                 entry (file mcg-org-password-file)
                 "* %U - %^{title} %^G\n\n - 用户名: %^{用户名}\n - 密码: %(get-or-create-password)"
                 :empty-lines 1
                 :kill-buffer t))

#+END_SRC

*** 3. Helper Functions
#+BEGIN_SRC emacs-lisp
  ;;; ====================================================================
  ;;; 3. 辅助函数
  ;;; ====================================================================
  ;; 参考: https://www.zmonster.me/2018/02/28/org-mode-capture.html

  ;; 密码生成函数
  (defun random-alphanum ()
    "生成随机字母或数字字符。"
    (let* ((charset "abcdefghijklmnopqurstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789")
           (x (random 62)))
      (char-to-string (elt charset x))))

  (defun create-password ()
    "生成 16 位随机密码。"
    (let ((value ""))
      (dotimes (number 16 value)
        (setq value (concat value (random-alphanum))))))

  (defun get-or-create-password ()
    "获取或创建密码。如果用户不输入则自动生成。"
    (let ((password (read-string "Password: ")))
      (if (string= password "")
          (create-password)
        password)))

  ;; 账单相关函数
  (defun get-year-and-month ()
    "获取当前年月（中文格式）。"
    (list (format-time-string "%Y 年") (format-time-string "%m 月")))

  (defun find-month-tree ()
    "在 Org 文件中查找或创建当前年月的树状结构。"
    (let* ((path (get-year-and-month))
         (level 1)
         end)
    (unless (derived-mode-p 'org-mode)
      (error "Target buffer \"%s\" should be in Org mode" (current-buffer)))
    (goto-char (point-min))
    (dolist (heading path)
      (let ((re (format org-complex-heading-regexp-format
                        (regexp-quote heading)))
            (cnt 0))
        (if (re-search-forward re end t)
            (goto-char (line-beginning-position))
          (progn
            (or (bolp) (insert "\n"))
            (if (/= (point) (point-min)) (org-end-of-subtree t t))
            (insert (make-string level ?*) " " heading "\n"))))
      (setq level (1+ level))
      (setq end (save-excursion (org-end-of-subtree t t))))
    (org-end-of-subtree)))
#+END_SRC

*** 4. Hugo Integration
#+BEGIN_SRC emacs-lisp
  ;;; ====================================================================
  ;;; 4. Hugo 博客集成
  ;;; ====================================================================

  ;; 仅在启用 Hugo 集成时配置
  (when mcg-org-enable-hugo-integration
    (with-eval-after-load 'org-capture
      ;; Hugo 捕获模板生成函数
      (defun org-hugo-new-subtree-post-capture-template ()
        "Returns `org-capture' template string for new Hugo post.
See `org-capture-templates' for more information."
    (let* (
           (date (format-time-string (org-time-stamp-format :long :inactive) (org-current-time)))
           (title (read-from-minibuffer "Post Title: ")) ;Prompt to enter the post title
           (fname (org-hugo-slug title)))
      (mapconcat #'identity
                 `(
                   ,(concat "* TODO " title)
                   ":PROPERTIES:"
                   ,(concat ":EXPORT_FILE_NAME: " fname)
                   ;;,(concat ":EXPORT_FILE_NAME: " "index")
                   ,(concat ":EXPORT_DATE: " date)
                   ":END:"
                   "%?\n")          ;Place the cursor here finally
                 "\n")))


      ;; Hugo 博客捕获模板
      (add-to-list 'org-capture-templates '("h" "Hugo Post"))
      (add-to-list 'org-capture-templates '("hl" "About Development Language"))
      
      ;; 编程语言相关博客
      (dolist (topic '(("hla" "ArkTS" "ArkTS")
                       ("hle" "React" "React")
                       ("hlm" "Assembly" "Assembly")
                       ("hlr" "Rust" "Rust")
                       ("hlc" "C#" "CSharp")
                       ("hlv" "Vue" "Vue")))
        (add-to-list 'org-capture-templates
                     `(,(nth 0 topic)
                       ,(concat "About " (nth 1 topic))
                       entry
                       (file+headline mcg-org-blog-development-file ,(nth 2 topic))
                       (function org-hugo-new-subtree-post-capture-template))))
      
      ;; MCU 相关
      (add-to-list 'org-capture-templates
                   '("hlu" "About MCU"
                     entry
                     (file+headline mcg-org-blog-mcu-file "MCU")
                     (function org-hugo-new-subtree-post-capture-template)))
      
      ;; 其他类别
      (add-to-list 'org-capture-templates
                   '("he" "About Emacs"
                     entry
                     (file+olp mcg-org-blog-emacs-file "Emacs")
                     (function org-hugo-new-subtree-post-capture-template)))
      
      (add-to-list 'org-capture-templates
                   '("hp" "About Prattle"
                     entry
                     (file+olp mcg-org-blog-prattle-file "Prattle")
                     (function org-hugo-new-subtree-post-capture-template)))
      
      (add-to-list 'org-capture-templates
                   '("hT" "About Tools"
                     entry
                     (file+olp mcg-org-blog-tools-file "Tools")
                     (function org-hugo-new-subtree-post-capture-template))))

    ;; Easy Hugo 配置
    (with-eval-after-load 'easy-hugo
      (setq easy-hugo-postdir "content-org"
            easy-hugo-default-ext ".org"
            easy-hugo-url "https://www.gemc.club"
            easy-hugo-basedir (concat mcg-blog-base-directory "/")))

    ;; 辅助函数
    (defun choice-series ()
      "提示用户输入系列名称并返回。"
      (read-from-minibuffer "Choice Series: ")))

#+END_SRC

** Ends
#+BEGIN_SRC emacs-lisp

(provide 'init-capture-hugo)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init-capture-hugo.el ends here
#+END_SRC
