* init-ibuffer.el
:PROPERTIES:
:HEADER-ARGS: :tangle (concat temporary-file-directory "init-ibuffer.el") :lexical t
:END:

** Headers
#+begin_src emacs-lisp
;;; init-ibuffer.el --- Config buffer  -*- lexical-binding: t; -*-

;;; Commentary:

  #+end_src

** Require
#+begin_src emacs-lisp
;;; Require:
(require 'ibuffer)
;; the ibuffer-vc
(require 'ibuffer-vc)

;;; Code:
  #+end_src

** Code
#+begin_src emacs-lisp :tangle no
;; https://egh0bww1.com/posts/2022-03-17-18-buffer-manage-ibuffer-usage/#org-H-1

;(setq uniquify-buffer-name-style      'reverse)    ; 
;(setq uniquify-separator              " · ")       ; 修饰内容与 buffer 名字之间的间隔符
;(setq uniquify-after-kill-buffer-p    t)           ; 关闭 buffer 时是否刷新名字
;(setq uniquify-ignore-buffers-re      "^\\*")      ; 满足该表达式的 buffer 不会被修饰
#+end_src

** Init
#+begin_src emacs-lisp
;; Don't open other window when in a Ibuffer
(defun init-ibuffer-ibuffer()
  "Init ibuffer ibuffer."
  (interactive)
  (if (string= (buffer-name) "*Ibuffer*")
      (ibuffer-update nil t)
    (ibuffer)
    )
  )
#+end_src

** ibuffer
#+begin_src emacs-lisp
;; ibuffer formats
;; see ibuffer.el
(setq ibuffer-formats
      '((mark modified read-only locked
              " " (name 18 18 :left :elide)
	      " " (size 9 -1 :right)
	      " " (mode 16 16 :left :elide)
	      " " filename-and-process)))

;; the ibuffer-vc
(push
 '(mark modified read-only vc-status-mini
	" " (name 18 18 :left :elide)
        " " (size 9 -1 :right)
        " " (mode 16 16 :left :elide)
        " " (vc-status 16 16 :left)
        " " vc-relative-file)
 ibuffer-formats)


;; ibuffer fontification-alist
;; just copy from ibuffer.el
(setq ibuffer-fontification-alist
      '((10 buffer-read-only font-lock-constant-face)
	(15 (and buffer-file-name
		 (string-match ibuffer-compressed-file-name-regexp
			       buffer-file-name))
	    font-lock-doc-face)
	(20 (string-match "^\\*" (buffer-name)) font-lock-keyword-face)
	(25 (and (string-match "^ " (buffer-name))
		 (null buffer-file-name))
	    italic)
	(30 (memq major-mode ibuffer-help-buffer-modes) font-lock-comment-face)
	(35 (derived-mode-p 'dired-mode) font-lock-function-name-face)
	(40 (and (boundp 'emacs-lock-mode) emacs-lock-mode) ibuffer-locked-buffer)))

;; don't use custom save
(setq ibuffer-save-with-custom nil)

;; saved filters
(let (it)
  ;; add push here
  (push '("ecsrc"
	  (or
	   (file-extension . "c")
	   (file-extension . "h"))
	  (or
	   (directory . "emacs.*src")
	   (directory . "emacs.*lib")))
	it)
  (push '("el"
	  (or
	   (file-extension . "el")
	   (used-mode . emacs-lisp-mode)))
	it)
  (push '("cl"
	  (or
	   (used-mode . lisp-mode)
	   (file-extension . "lisp")))
	it)
  (push '("esrc"
	  (and
	   (file-extension . "el")
	   (directory . "emacs/.*/lisp")))
	it)
  (push '("org"
	  (or
	   (file-extension . "org")
	   (used-mode . org-mode)))
	it)
  (push '("temp"
	  (starred-name))
	it)
  ;; set option to it
  (setq ibuffer-saved-filters it)
  )

;; saved groups
(let (it)
  ;;add push here
  (push '("default"
	  ("ecsrc"
	   (saved . "ecsrc"))
	  ("clisp"
	   (saved . "cl"))
	  ("src"
	   (saved . "esrc"))
	  ("elisp"
	   (saved . "el"))
	  ("org"
	   (saved . "org"))
	  ("temp"
	   (saved . "temp")))
	it)
  ;;set option to it
  (setq ibuffer-saved-filter-groups it)
  )

;; switch to default group when start ibuffer
(defun init-ibuffer-use-default-group ()
  (and (not ibuffer-filter-groups) ;; not use group
       (assoc "default" ibuffer-saved-filter-groups)
       (ibuffer-switch-to-saved-filter-groups "default")))
(add-hook 'ibuffer-hook 'init-ibuffer-use-default-group)

;; functions used to save filter and group config code
(defmacro init-ibuffer-generate-saver (name var)
  `(defun ,name (name)
     (interactive
      (if (null ,var)
	  (error "No item saved")
	(list (completing-read "get group: " ,var nil t))))
     (insert (concat "(push '"
		     (pp-to-string (assoc name ,var))
		     " it)"))))
(init-ibuffer-generate-saver init-ibuffer-filter ibuffer-saved-filters)
(init-ibuffer-generate-saver init-ibuffer-group ibuffer-saved-filter-groups)

;; preds and regexps that buffer not show
(setq ibuffer-never-show-predicates '())

;; preds and regexps that buffer show
(setq ibuffer-always-show-predicates '())

;; major mode never mark by content
(setq ibuffer-never-search-content-mode '(dired-mode))

;; don't show empty filter groups
(setq ibuffer-show-empty-filter-groups nil)

;; use other windows ibuffer
(setq ibuffer-use-other-window t)

;; use full size ibuffer
(setq ibuffer-default-shrink-to-minimum-size nil)

;; don't show summary
(setq ibuffer-display-summary nil)

;; enable cycle movement
(setq ibuffer-movement-cycle t)

;;config for ibuffer-vc
;;copied from ibuffer-vc.el
(defun init-ibuffer-group-by-vc-and-default ()
  (interactive)
  (let* ((vc-res (ibuffer-vc-generate-filter-groups-by-vc-root))
	 (new-group (append vc-res (cdr (assoc "default" ibuffer-saved-filter-groups)))))
    (setq ibuffer-filter-groups new-group)
    (let ((ibuf (get-buffer "*Ibuffer*")))
      (when ibuf
        (with-current-buffer ibuf
          (pop-to-buffer ibuf)
          (ibuffer-update nil t))))))
(define-key ibuffer-mode-map (kbd "/ '") 'init-ibuffer-group-by-vc-and-default)
#+end_src
** Ends
#+begin_src emacs-lisp
(provide 'init-ibuffer)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; init-ibuffer.el ends here
  #+end_src
